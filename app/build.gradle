apply plugin: 'com.android.application'

ext {
    supportLibVersion = '27.1.1'
}

android {
    compileSdkVersion 27
    signingConfigs {
        config {
            storeFile file("$rootDir/tindog-keystore.jks")
        }
    }
    defaultConfig {
        applicationId "com.tindog"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        buildConfigField("String", "firebaseEmail", firebaseEmail)
        buildConfigField("String", "firebasePass", firebasePass)
        buildConfigField("String", "mapsApiKey", mapsApiKey)
        buildConfigField("String", "adMobAppId", adMobAppId)
        buildConfigField("String", "adUnitId", adUnitId)
        resValue("string", "mapsApiKey", mapsApiKey)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

}

//Signing gitignore method inspired by: https://gist.github.com/SleeplessByte/89c90f702c0f2fdc2b12
File signingPropertiesFile = file("$rootDir/signing.properties")
if (signingPropertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(signingPropertiesFile))

    if (properties.containsKey('STORE_PASSWORD') && properties.containsKey('KEY_ALIAS') && properties.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.config.storePassword = properties['STORE_PASSWORD']
        android.signingConfigs.config.keyAlias = properties['KEY_ALIAS']
        android.signingConfigs.config.keyPassword = properties['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}

dependencies {
    //Android dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.android.support:support-v4:${supportLibVersion}"
    //implementation "com.android.support:support-compat:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation 'com.android.support:multidex:1.0.3'

    //Google dependencies
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.firebase:firebase-database:16.0.1'
    implementation 'com.google.firebase:firebase-storage:16.0.1'
    implementation 'com.google.firebase:firebase-auth:16.0.2'
    implementation 'com.google.firebase:firebase-ads:15.0.1'
    implementation 'com.firebaseui:firebase-ui-auth:4.0.0'

    //Test dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //3rd party library dependencies
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    //implementation 'com.google.android.exoplayer:exoplayer:2.8.1' //Not implementing exoplayer for now, since the app's main purpose is not to be a media player (the user can use other apps in the phone for that)
    implementation 'com.github.santalu:aspect-ratio-imageview:1.0.4'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.+'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}

apply plugin: 'com.google.gms.google-services'
